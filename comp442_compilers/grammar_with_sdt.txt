prog					-> #createGlobalTable# classDeclList progBody #endGlobalTable#
classDecl				-> class id #storeId# #createClassEntryAndTable# { varFuncDefList } ; #endClassEntryAndTable#
classDeclList			-> classDecl classDeclList 
classDeclList			-> EPSILON 
progBody				-> program #storeId# #createProgramTable# funcBody ; #endProgramTable# funcDefList 

funcDef					-> type #storeType# id #storeId# funcDefRest
funcDefRest				-> #startFuncDef# ( fParams ) #createFuncEntryAndTable# funcBody ; #endFuncEntryAndTable#
funcDefList				-> funcDef funcDefList 
funcDefList				-> EPSILON 
funcBody				-> { varStatementList } 

varDecl					-> type #storeType# id #storeId# varDeclRest
varDeclNoId				-> typeNoId #storeType# id #storeId# varDeclRest
varDeclRest				-> arraySizeList ; #createVariableEntry#

varFuncDefList			-> type #checkCircular# #storeType# id #storeId# varFuncDefListRest varFuncDefList
varFuncDefList			-> EPSILON
varFuncDefListRest		-> varDeclRest
varFuncDefListRest		-> funcDefRest

varStatementList		-> id varStatementListRest varStatementList
varStatementList		-> varDeclNoId varStatementList
varStatementList		-> noIdStartStatement varStatementList
varStatementList		-> EPSILON
varStatementListRest	-> #checkTypeGlobal# #storeType# id #storeId# varDeclRest 
varStatementListRest	-> #pushStatement# #addIdExprFragment# assignStatRest ; #popStatement#

statement				-> #pushStatement# noIdStartStatement #popStatement#
statement				-> #pushStatement# assignStat ; #popStatement#
statementList			-> statement statementList 
statementList			-> EPSILON 
noIdStartStatement      -> #forStatement# for ( type #storeType# id #storeId# #createVariableEntry# assignOp expr ; relExpr ; assignStat ) statBlock ; 
noIdStartStatement      -> #ifelseStatement# if ( expr ) then statBlock elseBlock ; 
noIdStartStatement      -> #getStatement# get ( variable #checkVar# ) ; 
noIdStartStatement      -> #putStatement# put ( expr ) ; 
noIdStartStatement      -> #returnStatement# return ( expr ) ;
elseBlock				-> else statBlock
elseBlock				-> EPSILON
assignStat				-> id #addIdExprFragment# assignStatRest
assignStat				-> assignStatRest
assignStatRest			-> indiceList assignStatRestRest
assignStatRestRest		-> . variable #checkVar# assignOp expr #checkExpr#
assignStatRestRest		-> #checkVar# assignOp expr #checkExpr#
statBlock				-> { statementList } 
statBlock				-> statement 
statBlock				-> EPSILON 

expr					-> #pushExpr# arithExpr exprRest #popExpr#
exprRest 				-> relOp arithExpr 
exprRest 				-> EPSILON
relExpr					-> arithExpr relOp arithExpr


sign					-> + #addSignExprFragment#
sign					-> - #addSignExprFragment#

arithExpr				-> term arithExprRest
arithExprRest 			-> addOp #operatorExprFragment# term arithExprRest
arithExprRest 			-> EPSILON
term					-> factor termRest
termRest 				-> multOp #operatorExprFragment# factor termRest
termRest				-> EPSILON
factor					-> ( arithExpr ) 
factor					-> num #addNumericExprFragment#
factor					-> not factor 
factor					-> sign factor 
factor					-> id #addIdExprFragment# factorRest
factorRest				-> dotidnest factorRest 
factorRest				-> #setFuncExprFragment# ( aParams )
factorRest				-> indice factorRestRest 
factorRest				-> EPSILON
factorRestRest			-> #addIdExprFragment# indice factorRestRest 
factorRestRest			-> dotidnest factorRest
factorRestRest			-> EPSILON

arithExprAEInt			-> termAEInt arithExprAEIntRest
arithExprAEIntRest		-> addOp #operatorExprFragment# termAEInt arithExprAEIntRest
arithExprAEIntRest		-> EPSILON
termAEInt				-> factorAEInt termAEIntRest
termAEIntRest			-> multOp #operatorExprFragment# factorAEInt termAEIntRest
termAEIntRest			-> EPSILON
factorAEInt				-> #setFuncExprFragment# ( arithExprAEInt )
factorAEInt				-> integer #addNumericExprFragment#
factorAEInt				-> not factorAEInt
factorAEInt				-> sign factorAEInt
factorAEInt				-> id #addIdExprFragment# factorRest

variable				-> #pushVar# id #addIdExprFragment# dotidIndiceList #popVar#
dotidnest				-> . id #addIdExprFragment#
dotidIndice				-> dotidnest
dotidIndice				-> indice
dotidIndiceList			-> dotidIndice dotidIndiceList
dotidIndiceList			-> EPSILON 
indice					-> [ arithExprAEInt ] 
indiceList				-> indice indiceList 
indiceList				-> EPSILON 
arraySize				-> [ integer #storeArraySize# ] 
arraySizeList			-> arraySize arraySizeList 
arraySizeList			-> EPSILON 

typeNoId				-> float
typeNoId				-> int
type					-> typeNoId 
type					-> id #checkTypeGlobal#

fParams					-> #addParameter# type #storeType# id #storeId# arraySizeList fParamsTailList 
fParams					-> EPSILON 
fParamsTail				-> , #addParameter# type #storeType# id #storeId# arraySizeList 
fParamsTailList			-> fParamsTail fParamsTailList 
fParamsTailList			-> EPSILON 

aParams					-> expr aParamsTailList 
aParams					-> EPSILON 
aParamsTail				-> , expr 
aParamsTailList			-> aParamsTail aParamsTailList 
aParamsTailList			-> EPSILON 

assignOp          		-> = #assignmentStatement#
relOp           		-> == 
relOp             		-> <>
relOp             		-> <
relOp           		-> >
relOp           		-> <=
relOp           		-> >=
addOp					-> + 
addOp					-> - 
addOp					-> or 
multOp					-> * 
multOp					-> / 
multOp					-> and 