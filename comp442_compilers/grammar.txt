prog            -> classDeclList progBody 
classDecl       -> class id { varFuncDefList } ; 
classDeclList   -> classDecl classDeclList 
classDeclList   -> EPSILON 
progBody        -> program funcBody ; funcDefList 

funcDef         -> type id funcDefRest
funcDefRest     -> ( fParams ) funcBody ;
funcDefList     -> funcDef funcDefList 
funcDefList     -> EPSILON 
funcBody        -> { varStatementList } 

varDecl         -> type id varDeclRest
varDeclNoId     -> typeNoId id varDeclRest
varDeclRest     -> arraySizeList ; 

varFuncDefList -> type id varFuncDefListRest varFuncDefList
varFuncDefList -> EPSILON
varFuncDefListRest -> varDeclRest
varFuncDefListRest -> funcDefRest

varStatementList -> id varStatementListRest varStatementList
varStatementList -> varDeclNoId varStatementList
varStatementList -> noIdStartStatement varStatementList
varStatementList -> EPSILON
varStatementListRest -> id varDeclRest 
varStatementListRest -> assignStatRest ;

statement -> noIdStartStatement
statement       -> assignStat ; 
statementList   -> statement statementList 
statementList   -> EPSILON 
noIdStartStatement       -> for ( type id assignOp expr ; relExpr ; assignStat ) statBlock ; 
noIdStartStatement       -> if ( expr ) then statBlock elseBlock ; 
noIdStartStatement       -> get ( variable ) ; 
noIdStartStatement       -> put ( expr ) ; 
noIdStartStatement       -> return ( expr ) ;
elseBlock -> else statBlock
elseBlock -> EPSILON
assignStat      -> id assignStatRest
assignStat      -> assignStatRest
assignStatRest -> indiceList assignStatRestRest
assignStatRestRest ->  . variable assignOp expr 
assignStatRestRest ->  assignOp expr 
statBlock       -> { statementList } 
statBlock       -> statement 
statBlock       -> EPSILON 


expr            -> arithExpr exprRest
exprRest 		-> relOp arithExpr | EPSILON
relExpr         -> arithExpr relOp arithExpr

arithExpr       -> term 
arithExpr       -> arithExpr addOp term 
sign            -> + 
sign            -> - 
term            -> factor 
term            -> term multOp factor 
factor          -> ( arithExpr ) 
factor          -> num 
factor          -> not factor 
factor          -> sign factor 
factor 			-> id factorIdRest
factorIdRest 	-> indiceList factorIdRestRest
factorIdRest 	-> ( aParams )
factorIdRestRest -> . id idnestList factorIdRest | EPSILON

variable        -> idnestList id indiceList 
idnest          -> id indiceList . 
idnestList      -> idnest idnestList 
idnestList      -> EPSILON 
indice          -> [ arithExpr ] 
indiceList      -> indice indiceList 
indiceList      -> EPSILON 
arraySize       -> [ integer ] 
arraySizeList   -> arraySize arraySizeList 
arraySizeList   -> EPSILON 

typeNoId -> float
typeNoId -> int
type            -> typeNoId 
type            -> id

fParams         -> type id arraySizeList fParamsTailList 
fParams         -> EPSILON 
fParamsTail     -> , type id arraySizeList 
fParamsTailList -> fParamsTail fParamsTailList 
fParamsTailList -> EPSILON 

aParams         -> expr aParamsTailList 
aParams         -> EPSILON 
aParamsTail     -> , expr 
aParamsTailList -> aParamsTail aParamsTailList 
aParamsTailList -> EPSILON 

assignOp          	 -> = 
relOp             	 -> < relOpLessThanRest
relOpLessThanRest 	 -> = | > | EPSILON
relOp           	 -> = = 
relOp           	 -> > relOpGreaterThanRest
relOpGreaterThanRest -> = | EPSILON
addOp           -> + 
addOp           -> - 
addOp           -> or 
multOp          -> * 
multOp          -> / 
multOp          -> and 

